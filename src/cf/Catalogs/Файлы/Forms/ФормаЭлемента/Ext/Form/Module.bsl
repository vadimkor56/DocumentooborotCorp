&НаКлиенте
Процедура ЗагрузитьФайл()
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
		
	ЗавершениеОбратныйВызов = Новый ОписаниеОповещения("ЗавершениеОбратныйВызов", ЭтотОбъект);
		
	НачатьПомещениеФайлаНаСервер(
		ЗавершениеОбратныйВызов,
		,,,
		ПараметрыДиалога,
		ЭтаФорма.УникальныйИдентификатор
	);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОбратныйВызов(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт  
	Если ОписаниеПомещенногоФайла = Неопределено Или ОписаниеПомещенногоФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = ОписаниеПомещенногоФайла.СсылкаНаФайл.Имя;
	СсылкаНаФайл = ОписаниеПомещенногоФайла.Адрес;
	ЯвляетсяКартинкой = РаботаСФайлами.ЯвляетсяКартинкой(Объект.Наименование);
	
	Если Не ЯвляетсяКартинкой Тогда
		Элементы.Предпросмотр.Видимость = Ложь;
	Иначе
		Элементы.Предпросмотр.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(СсылкаНаФайл) Тогда
		ТекущийОбъект.Хранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаФайл));
		ТекущийОбъект.Автор = РаботаСПользователями.ПолучитьТекущегоПользователя();
		ТекущийОбъект.Изменен = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЯвляетсяКартинкой = РаботаСФайлами.ЯвляетсяКартинкой(Объект.Наименование);
	Если ЯвляетсяКартинкой Тогда
		СсылкаНаФайл = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Хранилище");
	Иначе
		Элементы.Предпросмотр.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры